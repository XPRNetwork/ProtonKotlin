apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {
	compileSdkVersion 30
	buildToolsVersion "30.0.0"

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
	}

	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 30

		versionCode 11
		versionName "0.4.6"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		consumerProguardFiles 'consumer-rules.pro'
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

	// Unit Testing
	testImplementation "junit:junit:4.13"
	androidTestImplementation "androidx.test.ext:junit:1.1.1"
	androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"

	// KTX
	implementation "androidx.core:core-ktx:1.5.0-alpha01"

	// Lifecycle
	implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-alpha05"

	// Room
	implementation "androidx.room:room-ktx:2.2.5"
	kapt "androidx.room:room-compiler:2.2.5"

	// WorkManager
	implementation "androidx.work:work-runtime-ktx:2.3.4"

	// OkHttp
	implementation 'com.squareup.okhttp3:okhttp:4.7.2'
	implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'

	// Retrofit
	implementation('com.squareup.retrofit2:retrofit:2.9.0') {
		exclude module: 'okhttp'
	}
	implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

	// Dagger
	implementation 'com.google.dagger:dagger:2.28.1'
	implementation 'com.google.dagger:dagger-android-support:2.28.1'
	kapt 'com.google.dagger:dagger-android-processor:2.28.1'
	kapt 'com.google.dagger:dagger-compiler:2.28.1'

	// Assisted Inject (Dagger)
	compileOnly "com.squareup.inject:assisted-inject-annotations-dagger2:0.5.2"
	kapt "com.squareup.inject:assisted-inject-processor-dagger2:0.5.2"

	// Coroutines
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.4"

	// Timber
	implementation "com.jakewharton.timber:timber:4.7.1"

	// Gson
	implementation "com.google.code.gson:gson:2.8.6"

	// Guava
	implementation "com.google.guava:guava:29.0-jre"

	// Greymass ESR
	//implementation "com.greymass:esrsdk:1.0.1"
}

project.afterEvaluate {
	publishing {
		publications {
			aar(MavenPublication) {
				groupId = 'com.metallicus'
				artifactId 'protonsdk'
				version = android.defaultConfig.versionName

				artifact bundleDebugAar

				pom.withXml {
					def dependenciesNode = asNode().appendNode('dependencies')
					configurations.implementation.allDependencies.each {
						if (it.group != null && it.name != null && it.version != null &&
							it.name != 'unspecified' && it.version != 'unspecified') {
							def dependencyNode = dependenciesNode.appendNode('dependency')
							dependencyNode.appendNode('groupId', it.group)
							dependencyNode.appendNode('artifactId', it.name)
							dependencyNode.appendNode('version', it.version)
						}
					}
				}
			}
		}
	}
}
